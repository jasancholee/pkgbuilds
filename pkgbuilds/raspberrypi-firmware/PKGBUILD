# Maintainer: Oleg Rakhmanov <oleg [at] archlinuxarm [dot] org>

pkgname=raspberrypi-firmware
pkgver=20191123
pkgrel=1
_commit=9238b8c5eb79590ccd0ef0284c8e49306219fcf0
pkgdesc="Firmware tools, libraries, and headers for Raspberry Pi"
groups=('rpi4-base')
arch=('armv6h' 'armv7h' 'aarch64')
url="https://github.com/raspberrypi/userland"
license=('custom')
makedepends=('cmake')
replaces=('raspberrypi-firmware-tools')
provides=('raspberrypi-firmware-tools')
options=(!strip)
source=("https://github.com/raspberrypi/userland/archive/${_commit}.tar.gz"
        "https://raw.githubusercontent.com/raspberrypi/firmware/master/opt/vc/LICENCE"
        '00-raspberrypi-firmware.conf')
md5sums=('d8119936dc305284ae01b1c503f66ef0'
         '86e53f5f5909ee66900418028de11780'
         '72e0d5818fc513ece1b964f25f7e7882')

build() {
  cd ${srcdir}/userland-${_commit}

  if [ "${CARCH}" == 'aarch64' ]; then
    cmake -DCMAKE_BUILD_TYPE=Release -DARM64=ON
  else
    cmake -DCMAKE_BUILD_TYPE=Release
  fi
  
  make
}

package() {
  mkdir -p "${pkgdir}"/opt/vc

  cd ${srcdir}/userland-${_commit}
  make install DESTDIR="${pkgdir}"
  cp ../LICENCE "${pkgdir}"/opt/vc
  rm -rf "${pkgdir}"/opt/vc/src

  # Create lib links
  mkdir -p "${pkgdir}"/etc/ld.so.conf.d/
  cp "${srcdir}/00-raspberrypi-firmware.conf" "${pkgdir}"/etc/ld.so.conf.d/
}
